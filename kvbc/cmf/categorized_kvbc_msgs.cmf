# Updates

Msg MerkleUpdatesData 1 {
    map string string kv
    list string deletes
}

Msg ValueWithExpirationData 2 {
    string value
    optional int64 expire_at
    bool stale_on_update
}

Msg KeyValueUpdatesData 3 {
    map string ValueWithExpirationData kv
    list string deletes
    bool calculate_root_hash
}

Msg ImmutableValueUpdate 4 {
    string data
    list string tags
}

Msg ImmutableUpdatesData 5 {
    # key -> value, [tag1, tag2...]
    map string ImmutableValueUpdate kv
    bool calculate_root_hash
}

# Updates info

Msg MerkleKeyFlag 1000 {
    bool deleted
}

Msg MerkleUpdatesInfo 1001 {
    map string MerkleKeyFlag keys
    fixedlist uint8 32 root_hash
    uint64 state_root_version
}

Msg KVKeyFlag 1002 {

    bool deleted
    bool stale_on_update
}

Msg KeyValueUpdatesInfo 1003 {
    map string KVKeyFlag keys
    optional fixedlist uint8 32 root_hash
}

Msg ImmutableUpdatesInfo 1004 {
    # key -> [tag1, tag2...]
    map string list string tagged_keys
    optional map string fixedlist uint8 32 tag_root_hashes
}

# Blocks

Msg BlockKey 2000 {
    uint64 block_id
}

Msg BlockData 2001 {
    uint64 block_id
    fixedlist uint8 32 parent_digest
    map string oneof{
        MerkleUpdatesInfo
        KeyValueUpdatesInfo
        ImmutableUpdatesInfo
    } categories_updates_info
}


Msg RawBlockData 2005 {
    fixedlist uint8 32 parent_digest
    
    map string oneof{
        MerkleUpdatesData
        KeyValueUpdatesData
	    ImmutableUpdatesData
    } category_updates

    map string optional fixedlist uint8 32 category_root_hash

}

# Misc

Msg BenchmarkMessage 3000 {
    string str
    fixedlist uint8 32 hash
    map string string map
}

# DB Key-Values

Msg KeyHash 4000 {
    fixedlist uint8 32 value
}

Msg VersionedKey 4001 {
    KeyHash key_hash
    uint64 version
}

Msg ImmutableDbValue 4002 {
    uint64 block_id
    string data
}

# Used to deserialize the version only from an ImmutableDbValue.
Msg ImmutableDbVersion 4003 {
    uint64 block_id
}
